" Options {{{
set nocompatible
set encoding=utf-8
set autoindent
set autoread
set backspace=indent,eol,start
set completeopt=menuone,preview
set display=lastline
set expandtab
set foldmethod=syntax
set ignorecase
set noinfercase
set incsearch
set smartcase
set keywordprg=:help
set laststatus=2
set linebreak
set mouse=a
set cursorline
set cursorlineopt=number
set nohlsearch
set nojoinspaces
set noruler
set noshowcmd
set noshowmode
set noswapfile
set viminfofile=$XDG_CACHE_HOME/viminfo
set title
set titlestring=%<%F
set nowrap
set number
set shiftround
set shiftwidth=2
set shortmess=aoOstTIcF
set sidescroll=1
set sidescrolloff=2
set signcolumn=yes
set smarttab
set softtabstop=2
set tabstop=2
set tags=./tags;,tags;
set undodir=~/.vim/tmp/undo
set undofile
set undolevels=10000
set updatetime=100
set history=1000
set wildmenu
set wildignorecase
set wildignore+=.DS_Store,Icon\?,*.dmg,*.git,*.pyc,*.o,*.obj,*.so,*.swp,*.zip
set belloff=all
set formatoptions+=j
set complete-=i
set viewoptions-=options
set sessionoptions-=options
set nrformats=bin,hex,blank
set ttimeout
set ttimeoutlen=100
set nolangremap
set path+=**
set t_vb=
set noerrorbells
set noequalalways
if has('macunix')
  set clipboard=unnamed
elseif has('unix')
  set clipboard=unnamedplus
endif

" Disable netrw before calling filetype
let g:loaded_netrw = 1
let g:loaded_netrwPlugin = 1

filetype plugin indent on
" }}}

" Appearance {{{
syntax enable

set list
set cmdheight=1

if !exists('g:tabline_custom_names')
    let g:tabline_custom_names = {}
endif

function! InsertRelativeFilePath()
  let l:cwd = getcwd()
  let l:file = expand('%:p')
  if l:file == ''
    echo "No file associated with this buffer"
    return
  endif
  let l:relative_path = substitute(l:file, l:cwd . '/', '', '')
  execute "normal! i" . "[" . l:relative_path . "] \<ESC>"
endfunction

function! CopyRelativeFilePath()
  let l:root = getcwd()
  let l:file = expand('%:p')
  if l:file == ''
    echo "No file associated with this buffer"
    return
  endif
  let l:relative_path = substitute(l:file, l:root . '/', '', '')
  let @+ = l:relative_path
  echo "Copied: " . l:relative_path
endfunction

function! MyTabLine()
  let s = ''
  for i in range(tabpagenr('$'))
    " Select the highlighting
    if i + 1 == tabpagenr()
      let s .= '%#TabLineSel#'
    else
      let s .= '%#TabLine#'
    endif
    " Set the tab page number (for mouse clicks)
    let s .= '%' . (i + 1) . 'T'
    " Get the buffer list and window number for the tab page
    let buflist = tabpagebuflist(i + 1)
    let winnr = tabpagewinnr(i + 1)
    " Determine the name to use: custom or file name
    if has_key(g:tabline_custom_names, i + 1)
      let name = g:tabline_custom_names[i + 1]
    else
      let name = fnamemodify(bufname(buflist[winnr - 1]), ':t')
      if empty(name)
        let name = ' ? '
      endif
    endif
    " Append the name to the tab line
    let s .= ' ' . name . ' '
  endfor
  " Fill the rest of the tabline
  let s .= '%#TabLineFill#%T'
  return s
endfunction

set tabline=%!MyTabLine()

" Command to rename the current tab
command! -nargs=1 RenameTab call RenameCurrentTab(<q-args>)

function! RenameCurrentTab(new_name)
    " Store the new name in the dictionary for the current tab
    let g:tabline_custom_names[tabpagenr()] = a:new_name
endfunction

if has('termguicolors') && $COLORTERM ==# 'truecolor'
  let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
  let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"
  set termguicolors
endif

let &showbreak='└ '
let &fillchars='eob: '
let &listchars='tab:▏ ,trail:·,extends:>,precedes:<,nbsp:+'
let &t_SI="\e[5 q"
let &t_SR="\e[4 q"
let &t_EI="\e[2 q"

colorscheme gman
" }}}

" Mappings {{{
let mapleader = "\<Space>"
let maplocalleader = "\\"

" Tab & Window Management
nnoremap <silent> <C-w>h                :WinMoveOrSplit h<CR>
nnoremap <silent> <C-w>j                :WinMoveOrSplit j<CR>
nnoremap <silent> <C-w>k                :WinMoveOrSplit k<CR>
nnoremap <silent> <C-w>l                :WinMoveOrSplit l<CR>
nnoremap <silent> <C-A-h>               :vertical resize -5<CR>
nnoremap <silent> <C-A-j>               :resize +5<CR>
nnoremap <silent> <C-A-k>               :resize -5<CR>
nnoremap <silent> <C-A-l>               :vertical resize +5<CR>
nnoremap <silent> yoz                   <Plug>ZoomToggle
nnoremap          H                     gT
nnoremap          L                     gt

" close buffer but keep the split
nnoremap <silent> <leader>q             :b#<bar>bd#<CR>
" allows navigating wrapped lines with j,k
nnoremap          j                     gj
nnoremap          k                     gk
" delete/change the word under cursor and enables more
" deletes/changes by hitting (n)ext followed by . (dot)
nnoremap <silent> c*                    /\<<C-R>=expand('<cword>')<CR>\>\C<CR>``cgn
nnoremap <silent> c#                    ?\<<C-R>=expand('<cword>')<CR>\>\C<CR>``cgN
nnoremap <silent> d*                    /\<<C-r>=expand('<cword>')<CR>\>\C<CR>``dgn
nnoremap <silent> d#                    ?\<<C-r>=expand('<cword>')<CR>\>\C<CR>``dgN

nnoremap          Q                     :qa<CR>
nnoremap          Y                     y$
nnoremap          R                     :%s//g<Left><Left>
" Clear spaces on empty lines
nnoremap <silent> <leader>]             :%s/^[ ]\+$//<CR>

nnoremap <silent> <C-w>{                :pclose<CR>
nnoremap <silent> <C-g>                 2<C-g>
nnoremap <silent> <C-e>                 :e#<CR>
nnoremap <silent> <leader>w             :w<CR>
nnoremap          <leader>b             :buffer<Space>
nnoremap          <leader>f             :vimgrep // %<Left><Left><Left>
nnoremap          <leader>/             :Rg<Space>
nnoremap <silent> <leader>ec            :e ~/.vim/vimrc<CR>
nnoremap <silent> <leader>re            :so ~/.vim/vimrc<CR>
nnoremap <silent> <leader><C-]>         <C-w><C-]><C-w>T
nnoremap <silent> <localleader>s        :g/\v\/\/\s+[A-Z]/norm f/;ll~<CR>
" increment/decrement all numbers in visually selected lines
xnoremap <silent> <localleader>d        :s/\d\+/\=submatch(0)-1/g<CR>
xnoremap <silent> <localleader>i        :s/\d\+/\=submatch(0)+1/g<CR>
nnoremap <silent> 0                     :ToggleMovement ^ 0<CR>
xnoremap <silent> K                     :m '<-2<CR>gv=gv
xnoremap <silent> J                     :m '>+1<CR>gv=gv
inoremap          <C-w>                 <C-g>u<C-w>
inoremap          <C-u>                 <C-g>u<C-u>
" }}}

" Plugins {{{
nnoremap <silent> <leader>g             :Git<CR>
nnoremap <silent> <leader>p             :Files<CR>
nnoremap <silent> <leader>.             :GFiles<CR>
nnoremap <silent> <leader>r             :History:<CR>
nnoremap <silent> <leader>u             :UndotreeToggle<CR>
nnoremap <silent> <leader>s             :PicolineToggle<CR>
nnoremap <silent> <leader>cs            :call css_color#toggle()<CR>
nnoremap <silent> <leader>hd            :SignifyHunkDiff<CR>
nnoremap <silent> <leader>hu            :SignifyHunkUndo<CR>
nnoremap          <leader>tb            :Tagbar<CR>
nnoremap          <leader>tt            :Tags<CR>
nnoremap <silent> yoc                   :ContextToggle<CR>
nnoremap <silent> yop                   :IndentLinesToggle<CR>
nnoremap <silent> <localleader>p        :Goyo<CR>
" Quick copies
nnoremap          <leader>yf            :let @+ = expand('%:p')<CR>
nnoremap          <leader>yt            :let @+ = strftime("%Y-%m-%d")<CR>
nnoremap          <leader>ir            :call InsertRelativeFilePath()<CR>
nnoremap          <leader>yr            :call CopyRelativeFilePath()<CR>
nnoremap          <localleader>o        :Dispatch! open (pbpaste)<CR>
nnoremap          gz                    <Plug>(characterize)
nnoremap          ga                    <Plug>(EasyAlign)
xnoremap          ga                    <Plug>(EasyAlign)
xnoremap <silent> <leader><Tab>         <Plug>(fzf-maps-x)
onoremap <silent> <leader><Tab>         <Plug>(fzf-maps-o)
inoremap <silent> <C-x><C-k>            <Plug>(fzf-complete-word)
inoremap <silent> <C-x><C-l>            <Plug>(fzf-complete-line)
inoremap <silent> <C-x><C-f>            <Plug>(fzf-complete-path)
" Man {{{
runtime ftplugin/man.vim
" }}}
" MatchIt {{{
packadd! matchit
" }}}
" cfilter {{{
packadd! cfilter
" }}}
" comment {{{
packadd comment
" }}}
" FZF {{{
" Add fzf to runtime path based on OS
let g:fzf_colors = { 'border':  ['fg', 'FzfBorder', 'Ignore'] }
let g:fzf_layout = { 'window': { 'width': 0.9, 'height': 0.8, 'border': 'rounded' } }
let g:fzf_preview_window = ['right:70%:hidden', 'ctrl-]']
" }}}
" GH-Line {{{
let g:gh_line_map_default       = 0
let g:gh_line_blame_map_default = 0
let g:gh_repo_map               = '<leader><leader>go'
" }}}
" Signify {{{
let g:signify_sign_add = '▌'
let g:signify_sign_change = '▌'
let g:signify_sign_delete = '▌'
let g:signify_sign_change_delete = '∙'
" }}}
" Emmet {{{
let g:user_emmet_install_global = 0
" }}}
" indentLine {{{
let g:indentLine_fileType = ['yaml', 'yml', 'python', 'gdscript']
" }}}
" Undotree {{{
let g:undotree_DiffAutoOpen = 0
" }}}
" Dirvish {{{
let g:dirvish_mode = ':sort ,^.*[\/],'
" }}}
" Context.vim {{{
let g:context_enabled = 0
let g:context_add_mappings = 0
let g:context_highlight_tag = '<hide>'
" }}}
" vim-minisnip {{{
let g:minisnip_dir = '~/.vim/snippets'
let g:minisnip_trigger = '<C-j>'
" }}}
" vim-goyo {{{
let g:goyo_width = 120
let g:goyo_height = '95%'
" }}}
" }}}

" Autocommands {{{

augroup appearance_settings
  autocmd!
  autocmd VimEnter     * call echoraw(&t_EI)
  autocmd CmdlineEnter * call echoraw(&t_SI)
  autocmd CmdlineLeave * call echoraw(&t_EI)
augroup END

augroup quickfix
  autocmd!
  autocmd VimEnter        *     cwindow
  autocmd QuickFixCmdPost l*    lwindow
  autocmd QuickFixCmdPost [^l]* cwindow
  autocmd FileType        qf    map <buffer> dd :RemoveQfItem<CR>
augroup END

augroup filetype_settings
  autocmd!

  " Dirvish
  autocmd FileType dirvish nmap <buffer> <localleader>v .kitty icat<CR>
  autocmd FileType dirvish nmap <buffer> t :call dirvish#open('tabedit', 0)<CR>
  autocmd FileType dirvish xmap <buffer> t :call dirvish#open('tabedit', 0)<CR>

  " Fugitive
  autocmd FileType fugitive normal zR

  " HTTP
  autocmd FileType http nmap <buffer> <localleader>r :execute 'read !'.getline('.')<CR>

  " zed (spicedb)
  autocmd FileType zed setlocal noexpandtab

  " Man
  autocmd FileType man setlocal hlsearch
  autocmd FileType man nmap <buffer> q :q<CR>

  " Shell
  autocmd FileType sh,bash,zsh compiler shellcheck
  autocmd FileType sh,bash,zsh setlocal foldlevel=99

  " YAML
  autocmd FileType yaml setlocal foldlevel=99

  " HTML + CSS
  autocmd FileType html,css setlocal foldlevel=99
  autocmd FileType html,css EmmetInstall

  " C
  autocmd FileType c setlocal tabstop=4
  autocmd FileType c setlocal shiftwidth=4
  autocmd FileType c setlocal foldlevel=99
  autocmd FileType c nmap <buffer> <localleader>b :Dispatch! nob<CR>
  autocmd FileType c nmap <buffer> <localleader>t :Dispatch! ctags -R .<CR>

  " SQL
  autocmd FileType sql let g:stage_api = $STAGE_API_DB
  autocmd FileType sql let g:stage_dal = $STAGE_DAL_DB
  autocmd FileType sql nmap <buffer> <localleader>d :%DB g:local_dal

  " Proto
  autocmd FileType proto nmap <buffer> <localleader>l :Dispatch buf format -w<CR>

  " Nix
  autocmd FileType nix nmap <buffer> <localleader>r :Dispatch nix-instantiate --eval %<CR>

  " Go
  autocmd FileType go compiler go
  autocmd FileType go setlocal foldlevel=99
  autocmd FileType go nmap <buffer> <localleader>b :Dispatch go build %<CR>
  autocmd FileType go nmap <buffer> <localleader>t :Dispatch ctags --language-force=Go -R .<CR>
  autocmd FileType go nmap <buffer> <localleader>l :Dispatch gofmt -w %<CR>
  " or use: b:dispatch = go build %
  autocmd FileType go nmap <buffer> <localleader>m <Plug>FzfGoModules

  " Godot
  autocmd FileType   gdscript setlocal foldmethod=expr
  autocmd FileType   gdscript let      g:godot_executable = '/Applications/Godot.app/Contents/MacOS/Godot --position 1500,0'
  autocmd FileType   gdscript nmap <buffer> <localleader>r :GodotRun<CR>
  autocmd FileType   gdscript nmap <buffer> <localleader>b :Start! ./build.sh<CR>
  autocmd FileType   gdscript nmap <buffer> <localleader>c :GodotRunCurrent<CR>
  autocmd FileType   gdscript nmap <buffer> <localleader>d :GodotDocs<CR>
  autocmd FileType   gdscript nmap <buffer> <localleader>t :Dispatch ctags --language-force=GDScript -R .<CR>
  autocmd BufRead    *.gdip   setlocal filetype=gdresource
  autocmd BufNewFile *.gdip   setlocal filetype=gdresource

  " Git
  autocmd FileType gitcommit,gitrebase setlocal spell
  autocmd FileType gitcommit,gitrebase setlocal bufhidden=delete

  " GLSL
  autocmd BufRead    *.vert,*.tesc,*.tese,*.glsl,*.geom,*.frag,*.comp,*.rgen,*.rmiss,*.rchit,*.rahit,*.rint,*.rcall set filetype=glsl
  autocmd BufNewFile *.vert,*.tesc,*.tese,*.glsl,*.geom,*.frag,*.comp,*.rgen,*.rmiss,*.rchit,*.rahit,*.rint,*.rcall set filetype=glsl

  " JSON/JSONC
  autocmd FileType json,jsonc setlocal foldlevel=99
  autocmd FileType json,jsonc setlocal conceallevel=2

  " Markdown
  autocmd FileType markdown setlocal foldlevel=99
  autocmd FileType markdown setlocal textwidth=80
  autocmd FileType markdown setlocal conceallevel=2
  autocmd FileType markdown setlocal nonumber
  autocmd FileType markdown setlocal wrap
  autocmd FileType markdown let g:markdown_fenced_languages = ['python','javascript','bash=sh','html','vim','conf','sql','proto']

  " Python
  autocmd FileType python nmap <buffer> <localleader>m <Plug>FzfPythonPackages
  autocmd FileType python nmap <buffer> <localleader>t :Dispatch ctags --language-force=Python -R .<CR>

  " JavaScript
  autocmd FileType javascript,javascriptreact setlocal tabstop=4
  autocmd FileType javascript,javascriptreact setlocal shiftwidth=4
  autocmd FileType javascript,javascriptreact setlocal foldlevel=99
  autocmd FileType javascript,javascriptreact setlocal suffixesadd=.js,.jsx,.ts,.tsx
  autocmd FileType javascript,javascriptreact nmap <buffer> <localleader>m <Plug>FzfNodeModules
  autocmd FileType javascript,javascriptreact nmap <buffer> <localleader>b :Dispatch tsc %<CR>
  autocmd FileType javascript,javascriptreact nmap <buffer> <localleader>l :Dispatch eslint %<CR>
  autocmd FileType javascript,javascriptreact nmap <buffer> <localleader>r :Dispatch -compiler=make node %<CR>
  autocmd FileType javascript,javascriptreact nmap <buffer> <localleader>t :Dispatch ctags --language-force=JavaScript -R .<CR>
  autocmd FileType javascript,javascriptreact EmmetInstall

  " TypeScript
  autocmd FileType typescript,typescriptreact setlocal tabstop=2
  autocmd FileType typescript,typescriptreact setlocal shiftwidth=2
  autocmd FileType typescript,typescriptreact setlocal foldlevel=99
  autocmd FileType typescript,typescriptreact setlocal suffixesadd=.js,.jsx,.ts,.tsx
  autocmd FileType typescript,typescriptreact nmap <buffer> <localleader>m <Plug>FzfNodeModules
  autocmd FileType typescript,typescriptreact nmap <buffer> <localleader>b :Dispatch tsc %<CR>
  autocmd FileType typescript,typescriptreact nmap <buffer> <localleader>l :Dispatch eslint %<CR>
  autocmd FileType typescript,typescriptreact nmap <buffer> <localleader>r :Dispatch -compiler=make ts-node %<CR>
  autocmd FileType typescript,typescriptreact nmap <buffer> <localleader>t :Dispatch ctags --language-force=TypeScript -R .<CR>
  autocmd FileType typescript,typescriptreact EmmetInstall

augroup END
" }}}

" vim: set sw=2 ts=2 sts=0 expandtab fdm=marker :nospell:
