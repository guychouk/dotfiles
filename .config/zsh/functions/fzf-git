#!/usr/bin/env zsh

# FZF â™¥ Git

_is_in_git_repo() {
  git rev-parse HEAD > /dev/null 2>&1
}

_fzf-down() {
  fzf --height 50% "$@"
}

# Changed files
function fgf() {
  _is_in_git_repo || return
  git -c color.status=always status --short |
  _fzf-down -m --ansi --nth 2..,.. \
    --preview '(git diff --color=always -- {-1} | sed 1,4d; cat {-1}) | head -500' |
  cut -c4- | sed 's/.* -> //'
}


# Branches
function fgb() {
  _is_in_git_repo || return
  git branch -a --color=always | grep -v '/HEAD\s' | sort |
  _fzf-down --ansi --multi --tac --preview-window right:70% \
    --preview 'git log --oneline --graph --date=short --color=always --pretty="format:%C(auto)%cd %h%d %s" $(sed s/^..// <<< {} | cut -d" " -f1) | head -'$LINES |
  sed 's/^..//' | cut -d' ' -f1 |
  sed 's#^remotes/##'
}

# Tags
function fgt() {
  _is_in_git_repo || return
  git tag --sort -version:refname |
  _fzf-down --multi --preview-window right:70% \
    --preview 'git show --color=always {} | head -'$LINES
}

# Commits
function fgc() {
  _is_in_git_repo || return
  git log --date=short --format="%C(green)%C(bold)%cd %C(auto)%h%d %s (%an)" --graph --color=always |
  _fzf-down --ansi --no-sort --reverse --multi --bind 'ctrl-s:toggle-sort' \
    --header 'Press CTRL-S to toggle sort' \
    --preview 'grep -o "[a-f0-9]\{7,\}" <<< {} | xargs git show --color=always | head -'$LINES |
  grep -o "[a-f0-9]\{7,\}"
}

# Remotes
function fgr() {
  _is_in_git_repo || return
  git remote -v | awk '{print $1 "\t" $2}' | uniq |
  _fzf-down --tac \
    --preview 'git log --oneline --graph --date=short --pretty="format:%C(auto)%cd %h%d %s" {1} | head -200' |
  cut -d$'\t' -f1
}

# Commits + Open in browser
function fghc() {
  local repo
  _is_in_git_repo || return
  repo=$(git config --get remote.origin.url | sed 's/[^:]*://' | sed 's/\.git//')
  open "https://github.com/$repo/commit/$(fgc)"
}

# Remote + Open in browser
function fghr() {
  pushd ~/Projects > /dev/null || exit
  find . -type d -maxdepth 1 \
        | parallel 'cd {}; git remote get-url origin 2> /dev/null' \
        | awk '{match($0, /:(.*)/, a); if(a[1]) printf "%s\n", a[1]}' \
        | _fzf-down --bind 'enter:abort+execute(open http://github.com/{})'
  popd > /dev/null || exit
}
